plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.4'
	id 'io.spring.dependency-management' version '1.1.7'
    id 'com.diffplug.spotless' version '6.25.0'
    id 'com.github.spotbugs' version '6.0.7'
    id 'maven-publish'
}

group = 'com.example.petstore'
version = '0.1.0'
base {
    archivesName = 'common-api'
}

ext {
    springCloudVersion = '2025.0.0'
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
    mavenLocal()
	mavenCentral()
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

dependencies {
    // Spring関連
    implementation ('org.springframework.boot:spring-boot-starter-web') {
        exclude module: 'spring-boot-starter-tomcat'
    }
    implementation 'org.springframework.boot:spring-boot-starter-jetty'
    implementation 'org.springframework.boot:spring-boot-autoconfigure'
    implementation 'org.springframework.security:spring-security-core'
    implementation 'jakarta.validation:jakarta.validation-api'
    implementation 'org.springframework:spring-tx'

    // RDBMS
    implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.3'

    // logging
    implementation 'org.aspectj:aspectjweaver'
    implementation 'org.slf4j:slf4j-api'
    implementation 'net.logstash.logback:logstash-logback-encoder:7.4'

    // ユーティティ
    implementation 'org.apache.commons:commons-lang3:3.14.0'
    implementation 'commons-codec:commons-codec:1.16.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.20.0'

    // Tools
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    implementation 'com.example.petstore:common-core:0.1.0'
}

tasks.withType(Copy) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

spotless {
    java {
        enforceCheck = false
        removeUnusedImports()
        googleJavaFormat()
        ignoreErrorForPath('swagger')
        lineEndings 'UNIX'
    }
}

spotbugs {
    excludeFilter   = file("$rootDir/spotbugs-exclude.xml")
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = project.property('group')
            artifactId = base.archivesName.get()
            version = project.property('version')
            from components.java
        }
    }
}

// タスク制御
compileJava.dependsOn tasks.spotlessApply

bootJar.enabled=false
jar.enabled=true
jar.archiveClassifier=''
